<html>
  <head>
    <title>Final Task</title>
    <meta http-equiv="content-type" charset="utf-8">
  </head>
  <body style="margin:0; width:1250">
    <div id="contents" style="width:80%; margin: 0 auto">
      <div id="sentence" style="margin:40">
	<center>
	  <font size="7">情報可視化論 Final Task</font>
	</center><br><br>
	<div align="right">
	  <font size="5">205X011X 菅 佑輔</font>
	</div>
	<br>
	<div align="left">
	  <font size="5">プログラムの拡張項目</font><br>
	  <font size="4">
	    <ul>
	      <li>描画するロブスターの色値をisovalueから独立させ, それぞれの値を調節できるようにした. 色値とisovalueは共にインターフェースにスライダを用いており, 初期値は128としている. 色値はレインボーカラーマップの色値を参照している.</li>
	      <li>使用するshaderをGroundo ShaderとPhong Shaderから選択できるようにした. インターフェースにはラジオボタンを使用している. 初期状態はGroundo Shaderである.</li>
	      <li>使用するreflectionをLambertian Reflection, Phong Reflection, Blinn Phong Reflection, Cook Torrance Reflectionから選択できるようにし, さらに各モデルのパラメータとなるAmbient Reflection cofficient, Diffuse Reflection cofficient, Specular Reflection cofficient, Shininess Exponentを調節できるようにした. インターフェースは, それぞれラジオボタンとスライダを使用している. 初期値状態はLambertian Reflectionで, パラメータの初期値は各Reflection cofficientは0.5, Shiness Exponentは50.0である.</li>
	    </ul>
	  </font>
	</div>
      </div>

      <div id="display" style="width: 80%; float:left; margin-bottom:40;"> 
      </div>
      
      <div id="controller" style="width: 20%; float:left; background-color: lightgray; border-radius: 10px;">
	<br>
	<label id="label_iso" style="font-family:Arial;"></label>
	<input type="range" min="0" max="1" step="0.01" value="0.5" id="isovalue"/> <br>
	
	<label id="label_col" style="font-family:Arial;"></label>
	<input type="range" min="0" max="1" step="0.01" value="0.5" id="color"/> <br><br>

	<from id="shaders">
          <input type="radio" name="shader" value="gouraud" checked="checked">Gouraud Shader<br>
          <input type="radio" name="shader" value="phong">Phong Shader<br>
	</from>
	<br>
	

	<from id="reflections">
          <input type="radio" name="reflection" value="Lambertian" checked="checked">LambertianReflection<br>
          <input type="radio" name="reflection" value="Phong">PhongReflection<br>
	  <input type="radio" name="reflection" value="BlinnPhong">BlinnPhongReflection<br>
	  <input type="radio" name="reflection" value="CookTorrance">CookTorranceReflection<br>
	</from>
	<br>
	Reflection Parameter
	<br>
	<label id="label_ka" style="font-family:Arial;"></label>
	<input type="range" min="0" max="1" step="0.01" value="0.5" id="ambient"/> <br>
	<label id="label_kd" style="font-family:Arial;"></label>
	<input type="range" min="0" max="1" step="0.01" value="0.5" id="diffuse"/> <br>
	<label id="label_ks" style="font-family:Arial;"></label>
	<input type="range" min="0" max="1" step="0.01" value="0.5" id="specular"/> <br>
	<label id="label_n" style="font-family:Arial;"></label>
	<input type="range" min="0" max="1" step="0.005" value="0.25" id="shininess"/> <br>

	<br><button id="change-status-button" style="font-family=Arial;">Apply</button><br>
	<br>
      </div>
    </div>

    <script src="three.min.js"></script>
    <script src="TrackballControls.js"></script>
    <script src="https://threejs.org/examples/js/math/Lut.js"></script>
    <script src="https://naohisas.github.io/KVS.js/Build/KVS.min.js"></script>
    <script src="https://naohisas.github.io/KVS.js/Build/KVS2THREE.min.js"></script>
    <script src="https://naohisas.github.io/KVS.js/Source/KVSLobsterData.js"></script>
    <script src="Bounds.js"></script>
    <script src="Isosurfaces.js"></script>
    <script src="final_task.js"></script>

    <script type="x-shader/x-vertex" id="gouraud.vert">
      varying vec3 point_color;
      varying vec4 point_position;
      varying vec3 normal_vector;
      uniform vec3 light_position;
      uniform vec3 camera_position;
      uniform vec3 m_color;
      uniform float ka_value;
      uniform float kd_value;
      uniform float ks_value;
      uniform float n_value;
      uniform int reflection_id;

      vec3 LambertianReflection(vec3 C, vec3 L, vec3 N )
      {
          float ka = ka_value;
          float kd = kd_value;

          float dd = max(dot(N, L), 0.0);
          float Ia = ka;
          float Id = kd * dd;
          return C * (Ia + Id);
      }

      vec3 PhongReflection( vec3 C, vec3 L, vec3 N )
      {
          float ka = ka_value;
          float kd = kd_value;
          float ks = ks_value;
          float n = n_value;
          vec3 R = reflect( -L, N );
          vec3 V = normalize( camera_position - point_position.xyz );

          float dd = max( dot( N, L ), 0.0 );
          float ds = pow( max( dot( R, V ), 0.0 ), n );
          if ( dd <= 0.0 ) { ds = 0.0; }
          float Ia = ka;
          float Id = kd * dd;
          float Is = ks * ds;
          return C * ( Ia + Id + Is );
      }

       vec3 BlinnPhongReflection( vec3 C, vec3 L, vec3 N )
      {
	  float ka = ka_value;
	  float kd = kd_value;
	  float ks = ks_value;
	  float n = n_value;

	  vec3 V = normalize( camera_position - point_position.xyz );
	  vec3 H = normalize( L + V );
	  float dd = max( dot( N, L ), 0.0 );
	  float ds = 0.0;
	  if ( dd > 0.0 )
	  {
	      ds = pow( max( dot( H, N ), 0.0 ), n );
	  }

	  float Ia = ka;
	  float Id = kd * dd;
	  float Is = ks * ds;
	  return C * ( Ia + Id + Is );
      }

      vec3 CookTorranceReflection( vec3 C, vec3 L, vec3 N)
      {
	  float ka = ka_value;
	  float kd = kd_value;
	  float ks = ks_value;
	  float m = 0.2; // roughness. [0:smooth, 1:rough]
	  float F0 = 0.2;

	  vec3 V = normalize( camera_position - point_position.xyz );
	  float dd = max( dot( N, L ), 0.0 );
	  float ds = 0.0;
	  if ( dd > 0.0 )
	  {
	      vec3 H = normalize( L + V );
	      float NL = dd;
	      float NH = dot( N, H );
	      float NV = dot( N, V );
	      float VH = dot( V, H );

	      // Beckmann distribution
	      float NH2 = NH * NH;
	      float NH4 = NH2 * NH2;
	      float D = ( 1.0 / ( 4.0 * m * m * NH4 ) ) * exp( ( NH2 - 1.0 ) / ( m * m * NH2 ) );

	      // Geometrical attenuation factor
	      float G = min( 1.0, min( 2.0 * NH * NV / VH, 2.0 * NH * NL / VH ) );

	      // Fresnel term
	      float n = ( 1.0 + sqrt( F0 ) ) / ( 1.0 - sqrt( F0 ) );
	      float c = VH;
	      float g = sqrt( n * n + c * c - 1.0 );
	      float gc0 = g - c;
	      float gc1 = g + c;
	      float cgc0 = c * gc1 - 1.0;
	      float cgc1 = c * gc0 + 1.0;
	      float f0 = ( gc0 * gc0 ) / ( gc1 * gc1 );
	      float f1 = 1.0 + ( cgc0 / cgc1 ) * ( cgc0 / cgc1 );
	      float F = 0.5 * f0 * f1;

	      // Specular component
	      ds = ( F / 3.14159265 ) * ( D * G / ( NL * NV ) );
	  }

	  float Ia = ka;
	  float Id = kd * dd;
	  float Is = ks * ds;
	  return C * ( Ia + Id + Is );
      }

      void main()
      {
          point_position = modelViewMatrix * vec4( position, 1.0 );
          normal_vector = normalMatrix * normal;
	  
          vec3 C = m_color;
          vec3 L = normalize( light_position - point_position.xyz );
	  vec3 N = normalize( normal_vector );

	  if(reflection_id == 0){
	      point_color = LambertianReflection( C, L, N );
	  }else if(reflection_id == 1){
	      point_color = PhongReflection( C, L, N );
	  }else if(reflection_id == 2){
	      point_color =  BlinnPhongReflection( C, L, N);
	  }else if(reflection_id == 3){
	      point_color = CookTorranceReflection( C, L, N);
	  }
          gl_Position = projectionMatrix * point_position;
      }
    </script>
    <script type="x-shader/x-fragment" id="gouraud.frag">
      varying vec3 point_color;
      void main()
      {
          gl_FragColor = vec4( point_color, 1.0 );
      }
    </script>

    
    <script type="x-shader/x-vertex" id="phong.vert">
      varying vec3 point_color;
      varying vec4 point_position;
      varying vec3 normal_vector;
      uniform vec3 m_color;

      void main()
      {
          point_color = m_color;
          point_position = modelViewMatrix * vec4( position, 1.0 );
          normal_vector = normalMatrix * normal;
          gl_Position = projectionMatrix * point_position;
      }
    </script>
    <script type="x-shader/x-fragment" id="phong.frag">
      varying vec3 point_color;
      varying vec4 point_position;
      varying vec3 normal_vector;
      uniform vec3 light_position;
      uniform vec3 camera_position;
      uniform float ka_value;
      uniform float kd_value;
      uniform float ks_value;
      uniform float n_value;
      uniform int reflection_id;

      vec3 LambertianReflection( vec3 C, vec3 L, vec3 N )
      {
          float ka = ka_value;
          float kd = kd_value;

          float dd = max(dot(N, L), 0.0);
          float Ia = ka;
          float Id = kd * dd;
          return C * (Ia + Id);
      }

      vec3 PhongReflection( vec3 C, vec3 L, vec3 N )
      {
          float ka = ka_value;
          float kd = kd_value;
          float ks = ks_value;
          float n = n_value;
	  
          vec3 R = reflect( -L, N );
          vec3 V = normalize( camera_position - point_position.xyz );
	  
          float dd = max( dot( N, L ), 0.0 );
          float ds = pow( max( dot( R, V ), 0.0 ), n );
          if ( dd <= 0.0 ) {
	      ds = 0.0;
	  }
          float Ia = ka;
          float Id = kd * dd;
          float Is = ks * ds;
          return C * ( Ia + Id + Is );
      }

      vec3 BlinnPhongReflection( vec3 C, vec3 L, vec3 N )
      {
	  float ka = ka_value;
          float kd = kd_value;
          float ks = ks_value;
	  float n = n_value;

	  vec3 V = normalize( camera_position - point_position.xyz );
	  vec3 H = normalize( L + V );
	  float dd = max( dot( N, L ), 0.0 );
	  float ds = 0.0;
	  if ( dd > 0.0 )
	  {
	      ds = pow( max( dot( H, N ), 0.0 ), n );
	  }

	  float Ia = ka;
	  float Id = kd * dd;
	  float Is = ks * ds;
	  return C * ( Ia + Id + Is );
      }

      vec3 CookTorranceReflection( vec3 C, vec3 L, vec3 N)
      {
	  float ka = ka_value;
          float kd = kd_value;
          float ks = ks_value;
	  float m = 0.2; // roughness. [0:smooth, 1:rough]
	  float F0 = 0.2;

	  vec3 V = normalize( camera_position - point_position.xyz );
	  float dd = max( dot( N, L ), 0.0 );
	  float ds = 0.0;
	  if ( dd > 0.0 )
	  {
	      vec3 H = normalize( L + V );
	      float NL = dd;
	      float NH = dot( N, H );
	      float NV = dot( N, V );
	      float VH = dot( V, H );

	      // Beckmann distribution
	      float NH2 = NH * NH;
	      float NH4 = NH2 * NH2;
	      float D = ( 1.0 / ( 4.0 * m * m * NH4 ) ) * exp( ( NH2 - 1.0 ) / ( m * m * NH2 ) );

	      // Geometrical attenuation factor
	      float G = min( 1.0, min( 2.0 * NH * NV / VH, 2.0 * NH * NL / VH ) );

	      // Fresnel term
	      float n = ( 1.0 + sqrt( F0 ) ) / ( 1.0 - sqrt( F0 ) );
	      float c = VH;
	      float g = sqrt( n * n + c * c - 1.0 );
	      float gc0 = g - c;
	      float gc1 = g + c;
	      float cgc0 = c * gc1 - 1.0;
	      float cgc1 = c * gc0 + 1.0;
	      float f0 = ( gc0 * gc0 ) / ( gc1 * gc1 );
	      float f1 = 1.0 + ( cgc0 / cgc1 ) * ( cgc0 / cgc1 );
	      float F = 0.5 * f0 * f1;

	      // Specular component
	      ds = ( F / 3.14159265 ) * ( D * G / ( NL * NV ) );
	  }

	  float Ia = ka;
	  float Id = kd * dd;
	  float Is = ks * ds;
	  return C * ( Ia + Id + Is );
      }

       void main()
      {
          vec3 C = point_color;
          vec3 L = normalize( light_position - point_position.xyz );
          vec3 N = normalize( normal_vector );

	  vec3 shaded_color;
	  if(reflection_id == 0){
              shaded_color = LambertianReflection( C, L, N );
	  }else if(reflection_id == 1){
	      shaded_color = PhongReflection( C, L, N);
	  }else if(reflection_id == 2){
	      shaded_color = BlinnPhongReflection( C, L, N);
	  }else if(reflection_id == 3){
	      shaded_color = CookTorranceReflection( C, L, N);
	  }
	  
          gl_FragColor = vec4( shaded_color, 1.0 );
      }
    </script>

    <script>
      main();
    </script>
  </body>
</html>
